buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'


processResources {
    duplicatesStrategy = duplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include "**/mcmod.info"
        expand("version": project.version)
    }
}

minecraft {
    mappings channel: "snapshot", version: "20171003-1.12"
}

dependencies {
    minecraft ("net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}")

    implementation ("net.kyori:adventure-api:4.17.0") {
        exclude(module: "adventure-bom")
        exclude(module: "annotations")
    }
    implementation ("net.kyori:adventure-text-serializer-gson:4.17.0") {
        exclude(module: "adventure-bom")
        exclude(module: "adventure-api")
        exclude(module: "annotations")
        exclude(module: "auto-service-annotations")
        exclude(module: "gson")
    }

    implementation project(path: ":common", configuration: "shadow")
}


shadowJar {
    dependencies {
        include(project(":common"))
        include(dependency("net.kyori:.*"))
    }

    exclude 'META-INF/**'
    exclude '**/module-info.class'

    relocate "net.kyori", "com.cssbham.cssminecraft.lib.adventure"

    archivesBaseName = "cssminecraft-forge"

    minimize()
}


reobf {
    shadowJar {}
}

shadowJar.finalizedBy('reobfShadowJar')
